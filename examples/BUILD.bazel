load("@bazel_binaries//:defs.bzl", "bazel_binaries")
load(
    "@rules_bazel_integration_test//bazel_integration_test:defs.bzl",
    "bazel_integration_test",
    "integration_test_utils",
)
load("@rules_shell//shell:sh_binary.bzl", "sh_binary")

sh_binary(
    name = "simple_test_runner",
    srcs = [":caching_test_runner.sh"],
)

## Simple

TEST_NAME = "simple"

bazel_integration_test(
    name = TEST_NAME + "_test",
    bazel_binaries = bazel_binaries,
    bazel_version = bazel_binaries.versions.current,
    test_runner = ":simple_test_runner",
    workspace_files = integration_test_utils.glob_workspace_files(TEST_NAME) + [
        "//:local_repository_files",
    ],
    workspace_path = TEST_NAME,
)

[
    bazel_integration_test(
        name = integration_test_utils.bazel_integration_test_name(
            TEST_NAME + "_test",
            bazel_version,
        ),
        bazel_binaries = bazel_binaries,
        bazel_version = bazel_version,
        test_runner = ":simple_test_runner",
        workspace_files = integration_test_utils.glob_workspace_files(TEST_NAME) + [
            "//:local_repository_files",
        ],
        workspace_path = TEST_NAME,
    )
    for bazel_version in bazel_binaries.versions.other
]

## All integration tests

# By default, the integration test targets are tagged as `manual`. This
# prevents the targets from being found from most target expansion (e.g.
# `//...`, `:all`). To easily execute a group of integration tests, you may
# want to define a test suite which includes the desired test targets.
test_suite(
    name = "all_integration_tests",
    # If you don't apply the test tags to the test suite, the test suite will
    # be found when `bazel test //...` is executed.
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = [":simple_test"] +
            integration_test_utils.bazel_integration_test_names(
                "simple_test",
                bazel_binaries.versions.other,
            ),
    visibility = ["//:__subpackages__"],
)
